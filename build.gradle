buildscript {
	dependencies {
		classpath 'de.guntram.mcmod:crowdin-translate:1.4+1.19-pre2'
	}
	repositories {
		maven {
			name = 'CrowdinTranslate source'
			url = "https://minecraft.guntram.de/maven/"
		}
	}
}

plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id 'maven-publish'
}

version = project.version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url 'https://minecraft.guntram.de/maven/' }
}

runClient.doFirst {
	args = ['--username', "MCLegoMan${new Random().nextInt(1000)}"]
}


dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft}"
	mappings "net.fabricmc:yarn:${project.mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api}"

	include(modImplementation "com.github.MCLegoMan:fabric-simplelibs:${project.fabricsimplelibs}")
	include(modImplementation "com.github.MCLegoMan:releasetypeutils:${project.releasetypeutils}")

	modImplementation "com.terraformersmc:modmenu:${project.modmenu}"
	modImplementation "com.github.Nettakrim:Souper-Secret-Settings:${project.souper_secret_settings}"

	modImplementation "de.guntram.mcmod:crowdin-translate:1.4+1.19.3"
	include "de.guntram.mcmod:crowdin-translate:1.4+1.19.3"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
	}
}

modrinth {
	token = rootProject.file(".env").text.readLines().get(0).replace("MODRINTH=", "").replace("\"", "")
	projectId = "${project.modrinth_id}"
	versionNumber = "${project.version}"
	versionType = "${project.version_type}"
	versionName = "${project.title}"
	uploadFile = remapJar
	gameVersions = ['1.20', '1.20.1']
	loaders = ['fabric', 'quilt']
	changelog = rootProject.file("CHANGELOG.md").text
	dependencies {
		required.project("P7dR8mSH") // Fabric-API
	}
	syncBodyFrom = rootProject.file("README.md").text
}
curseforge {
	apiKey = rootProject.file(".env").text.readLines().get(1).replace("CURSEFORGE=", "").replace("\"", "")
	project {
		id = "${project.curseforge_id}"
		changelog = rootProject.file("CHANGELOG.md").text
		releaseType = "${project.version_type}"
		gameVersionStrings = ['1.20', '1.20.1', "Java 17", "Fabric", "Quilt"]
		mainArtifact(jar) {
			displayName = "${project.title}"
		}
	}
	options {
	}
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'perspective'